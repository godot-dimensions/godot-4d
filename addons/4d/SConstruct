#!/usr/bin/env python

# This file is for building as a Godot GDExtension.

env = SConscript("src/godot-cpp/SConstruct")

# Add source files.
env.Append(
    CPPPATH=[
        "./,src/,../../",
        "../../math",
        "../../math/geometric_algebra",
        "../../nodes",
        "../../resources",
        "../../model",
        "../../model/g4mf",
        "../../model/g4mf/structures",
        "../../model/g4mf/structures/physics",
        "../../model/mesh",
        "../../model/mesh/poly",
        "../../model/mesh/tetra",
        "../../model/mesh/wire",
        "../../model/off",
        "../../physics",
        "../../physics/bodies",
        "../../physics/server",
        "../../physics/shapes",
        "../../render",
        "../../render/wireframe_canvas",
        "../../render/cross_section"
    ]
)

sources = (
    Glob("*.cpp")
    + Glob("src/*.cpp")
    + Glob("../../*.cpp")
    + Glob("../../math/*.cpp")
    + Glob("../../math/geometric_algebra/*.cpp")
    + Glob("../../nodes/*.cpp")
    + Glob("../../resources/*.cpp")
    + Glob("../../model/*.cpp")
    + Glob("../../model/g4mf/*.cpp")
    + Glob("../../model/g4mf/structures/*.cpp")
    + Glob("../../model/g4mf/structures/physics/*.cpp")
    + Glob("../../model/mesh/*.cpp")
    + Glob("../../model/mesh/poly/*.cpp")
    + Glob("../../model/mesh/tetra/*.cpp")
    + Glob("../../model/mesh/wire/*.cpp")
    + Glob("../../model/off/*.cpp")
    + Glob("../../physics/*.cpp")
    + Glob("../../physics/bodies/*.cpp")
    + Glob("../../physics/server/*.cpp")
    + Glob("../../physics/shapes/*.cpp")
    + Glob("../../render/*.cpp")
    + Glob("../../render/wireframe_canvas/*.cpp")
    + Glob("../../render/cross_section/*.cpp")
)


env.Append(CPPDEFINES=["GDEXTENSION"])

if env["target"] == "editor":
    env.Append(
        CPPPATH=[
            "../../editor",
            "../../editor/icons",
            "../../editor/import",
            "../../editor/import/g4mf",
            "../../editor/import/off",
            "../../editor/viewport",
        ]
    )
    sources += Glob("../../editor/*.cpp")
    sources += Glob("../../editor/icons/*.cpp")
    sources += Glob("../../editor/import/g4mf/*.cpp")
    sources += Glob("../../editor/import/off/*.cpp")
    sources += Glob("../../editor/viewport/*.cpp")

if env["platform"] == "windows":
    if env["OBJSUFFIX"].endswith(".obj"):
        env["OBJSUFFIX"] = env["OBJSUFFIX"][:-4] + ".o.obj"

bin_path = "bin/"
extension_name = "godot_4d"
debug_or_release = "release" if env["target"] == "template_release" else "debug"

if not "arch_suffix" in env:
    env["arch_suffix"] = env["arch"]

if env["target"] in ["editor", "template_debug"]:
    try:
        doc_data = env.GodotCPPDocData(
            "src/gen/doc_data.gen.cpp", source=Glob("doc_classes/*.xml")
        )
        sources.append(doc_data)
    except AttributeError:
        print("Not including class reference as we're targeting a pre-4.3 baseline.")

# Create the library target (e.g. libgodot_4d.linux.debug.x86_64.so).
if env["platform"] == "macos":
    library = env.SharedLibrary(
        "{0}/lib{1}.{2}.{3}.framework/{1}.{2}.{3}".format(
            bin_path,
            extension_name,
            env["platform"],
            debug_or_release,
        ),
        source=sources,
    )
else:
    library = env.SharedLibrary(
        "{}/lib{}.{}.{}.{}{}".format(
            bin_path,
            extension_name,
            env["platform"],
            debug_or_release,
            env["arch_suffix"],
            env["SHLIBSUFFIX"],
        ),
        source=sources,
    )

Default(library)

# The rest of this file handles generating the 4D icons file and GLSL header files.
import importlib.util
import os
import sys

header_builders_path = os.path.abspath(os.path.join("src/header_builders.py"))
spec = importlib.util.spec_from_file_location("header_builders", header_builders_path)
header_builders = importlib.util.module_from_spec(spec)
sys.modules["header_builders"] = header_builders
spec.loader.exec_module(header_builders)

env["BUILDERS"]["GLSL_HEADER"] = Builder(
    action=header_builders.build_raw_headers_action,
    suffix="glsl.gen.h",
)

env.GLSL_HEADER("../../render/cross_section/tetra_cross_section_shader.glsl")
env.GLSL_HEADER("../../render/cross_section/wireframe_cross_section_shader.glsl")

target_file_path = "../../editor/icons/editor_4d_icons.gen.h"
icon_sources = Glob("icons/4D.svg") + Glob("icons/Node4D.svg")
header_builders.make_svg_icons_action([target_file_path], icon_sources, env)
