#!/usr/bin/env python

Import("env")
Import("env_modules")

env_4d = env_modules.Clone()

env_4d.add_source_files(env.modules_sources, "*.cpp")
env_4d.add_source_files(env.modules_sources, "icons/*.cpp")
env_4d.add_source_files(env.modules_sources, "import/g4mf/*.cpp")
env_4d.add_source_files(env.modules_sources, "import/off/*.cpp")
env_4d.add_source_files(env.modules_sources, "viewport/*.cpp")

# The rest of this file handles generating the 4D icons header file.
import importlib.util
import os
import sys

header_builders_path = os.path.abspath(
    os.path.join("../addons/4d/src/header_builders.py")
)
spec = importlib.util.spec_from_file_location("header_builders", header_builders_path)
header_builders = importlib.util.module_from_spec(spec)
sys.modules["header_builders"] = header_builders
spec.loader.exec_module(header_builders)

env["BUILDERS"]["MakeSVGIconsBuilder"] = Builder(
    action=env.Run(header_builders.make_svg_icons_action),
    suffix=".h",
    src_suffix=".svg",
)

target_file_path = "icons/editor_4d_icons.gen.h"
icon_sources = Glob("../addons/4d/icons/4D.svg") + Glob("../addons/4d/icons/Node4D.svg")
env.Alias("editor_4d_icons", [env.MakeSVGIconsBuilder(target_file_path, icon_sources)])
